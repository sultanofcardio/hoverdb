group 'com.sultanofcardio'
version '3.0.1'

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'

buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    doclava
}

jar {
    baseName = "$project.name"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = "${project.name}"
    version = version
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = "${project.name}"
    version = version

    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar, javadocJar)

publishing {
    repositories {
        maven {
            name "sultanofcardio"
            credentials {
                username "${sultanofcardioUser}"
                password "${sultanofcardioPassword}"
            }
            url "${sultanofcardioUrl}"
        }

        maven {
            name "kiosk-mixed"
            credentials {
                username "${kioskMixedUser}"
                password "${kioskMixedPassword}"
            }
            url "${kioskMixedUrl}"
        }
    }

    publications {
        binary(MavenPublication) {
            artifactId "${project.name}"
            version version
            from components.java
            artifact javadocJar
        }

        snapshot(MavenPublication) {
            artifactId "${project.name}"
            version "${version}-SNAPSHOT"
            from components.java
            artifact javadocJar
        }

        full(MavenPublication) {
            artifactId "${project.name}"
            version version
            from components.java
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.sultanofcardio.com/artifactory/sultanofcardio'
    }
    maven {url 'http://clojars.org/repo/'}
}

dependencies {
    doclava 'com.google.doclava:doclava:1.0.6'
    doclava 'org.jetbrains:annotations:16.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.json:json:20180130'
    implementation 'org.jetbrains:annotations:16.0.1'

    compileOnly 'com.oracle:ojdbc6:sultan'
    compileOnly 'mysql:mysql-connector-java:5.1.45'
    compileOnly 'com.microsoft.sqlserver:sqljdbc4:4.0'
    compileOnly 'postgresql:postgresql:9.1-901-1.jdbc4'
    compileOnly 'org.xerial:sqlite-jdbc:3.27.2.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'ch.vorburger.mariaDB4j:mariaDB4j:2.4.0'
    testImplementation 'com.oracle:ojdbc6:sultan'
    testImplementation 'mysql:mysql-connector-java:5.1.45'
    testImplementation 'com.microsoft.sqlserver:sqljdbc4:4.0'
    testImplementation 'postgresql:postgresql:9.1-901-1.jdbc4'
    testImplementation 'org.xerial:sqlite-jdbc:3.27.2.1'
}

task generateJavadoc(type: Javadoc, dependsOn: project.configurations.doclava) {
    failOnError = true
    title = null
    source = sourceSets.main.java.srcDirs
    options.overview = "src/overview.html"
    options.doclet = "com.google.doclava.Doclava"
    options.docletpath = configurations.doclava.files.asType(List)
    classpath += sourceSets.main.runtimeClasspath
    destinationDir = file("build/docs/javadoc/${version}")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
